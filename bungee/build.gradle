plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

dependencies {
    compile project(':common')

    compileOnly 'net.md-5:bungeecord-api:1.14-SNAPSHOT'
    compileOnly 'net.kyori:text-adapter-bungeecord:3.0.2'
    compileOnly 'com.imaginarycode.minecraft:RedisBungee:0.4'

    // migration plugins
    compileOnly('net.alpenblock:BungeePerms:3.0') {
        exclude(module: 'bungeecord-api')
        exclude(module: 'lombok')
        exclude(module: 'spigot-api')
        exclude(module: 'worldedit')
        exclude(module: 'Vault')
        exclude(module: 'Essentials')
        exclude(module: 'EssentialsX')
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'pluginVersion': project.ext.fullVersion
        include 'plugin.yml'
    }
}

shadowJar {
    archiveName = "LuckPerms-Bungee-${project.ext.fullVersion}.jar"

    dependencies {
        include(dependency('net.luckperms:.*'))
        include(dependency('me.lucko.luckperms:.*'))
    }

    relocate 'net.kyori.text', 'me.lucko.luckperms.lib.text'
    relocate 'net.kyori.event', 'me.lucko.luckperms.lib.eventbus'
    relocate 'com.github.benmanes.caffeine', 'me.lucko.luckperms.lib.caffeine'
    relocate 'okio', 'me.lucko.luckperms.lib.okio'
    relocate 'okhttp3', 'me.lucko.luckperms.lib.okhttp3'
    relocate 'me.lucko.commodore', 'me.lucko.luckperms.lib.commodore'
    relocate 'org.mariadb.jdbc', 'me.lucko.luckperms.lib.mariadb'
    relocate 'com.mysql', 'me.lucko.luckperms.lib.mysql'
    relocate 'org.postgresql', 'me.lucko.luckperms.lib.postgresql'
    relocate 'com.zaxxer.hikari', 'me.lucko.luckperms.lib.hikari'
    relocate 'com.mongodb', 'me.lucko.luckperms.lib.mongodb'
    relocate 'org.bson', 'me.lucko.luckperms.lib.bson'
    relocate 'redis.clients.jedis', 'me.lucko.luckperms.lib.jedis'
    relocate 'org.apache.commons.pool2', 'me.lucko.luckperms.lib.commonspool2'
    relocate 'ninja.leaping.configurate', 'me.lucko.luckperms.lib.configurate'
}

artifacts {
    archives shadowJar
}
