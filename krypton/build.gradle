import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.31'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'application'
}

application.mainClassName = 'me.lucko.luckperms.krypton.LPKryptonBootstrap'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
//    maven { url 'https://repo.bristermitten.me/repository/maven-public/' }
    mavenLocal()
    maven { url 'https://libraries.minecraft.net' }
}

dependencies {
    implementation project(':common')

    // Krypton doesn't have these, so we have to shade them in manually
    implementation 'com.google.code.gson:gson:2.7'
    implementation 'com.google.guava:guava:19.0'

    compileOnly 'org.kryptonmc:krypton-api:0.13.9'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += ['-Xopt-in=kotlinx.serialization.ExperimentalSerializationApi']
    }
}

processResources {
    filter ReplaceTokens, tokens: ["version": project.ext.fullVersion]
}

shadowJar {
    archiveFileName.set("LuckPerms-Krypton-${project.ext.fullVersion}.jar")

    dependencies {
        include(dependency('net.luckperms:.*'))
        include(dependency('me.lucko.luckperms:.*'))
        include(dependency('com.google.guava:guava:.*'))
        include(dependency('com.google.code.gson:gson:.*'))
    }

    relocate 'com.google.common', 'me.lucko.luckperms.lib.guava'
    relocate 'com.google.gson', 'me.lucko.luckperms.lib.gson'

    relocate 'net.kyori.event', 'me.lucko.luckperms.lib.eventbus'
    relocate 'com.github.benmanes.caffeine', 'me.lucko.luckperms.lib.caffeine'
    relocate 'okio', 'me.lucko.luckperms.lib.okio'
    relocate 'okhttp3', 'me.lucko.luckperms.lib.okhttp3'
    relocate 'net.bytebuddy', 'me.lucko.luckperms.lib.bytebuddy'
    relocate 'org.mariadb.jdbc', 'me.lucko.luckperms.lib.mariadb'
    relocate 'com.mysql', 'me.lucko.luckperms.lib.mysql'
    relocate 'org.postgresql', 'me.lucko.luckperms.lib.postgresql'
    relocate 'com.zaxxer.hikari', 'me.lucko.luckperms.lib.hikari'
    relocate 'com.mongodb', 'me.lucko.luckperms.lib.mongodb'
    relocate 'org.bson', 'me.lucko.luckperms.lib.bson'
    relocate 'redis.clients.jedis', 'me.lucko.luckperms.lib.jedis'
    relocate 'com.rabbitmq', 'me.lucko.luckperms.lib.rabbitmq'
    relocate 'org.apache.commons.pool2', 'me.lucko.luckperms.lib.commonspool2'
    relocate 'ninja.leaping.configurate', 'me.lucko.luckperms.lib.configurate'
}

artifacts {
    archives shadowJar
}