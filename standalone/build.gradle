plugins {
    alias(libs.plugins.shadow)
    id("java-library")
    id("jacoco")
    id("jacoco-report-aggregation")
}

sourceCompatibility = 17
targetCompatibility = 17

test {
    useJUnitPlatform {
        if (!project.hasProperty('dockerTests')) {
            excludeTags 'docker'
        }
    }
    systemProperty('net.kyori.ansi.colorLevel', 'indexed16')
}

jacocoTestReport {
    dependsOn test
}

dependencies {
    api project(':library')

    api 'org.apache.logging.log4j:log4j-core:2.17.2'
    api 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.2'
    api 'net.minecrell:terminalconsoleappender:1.3.0'
    api 'org.jline:jline-terminal-jansi:3.20.0'

    api 'io.netty:netty-all:4.2.1.Final'

    api('net.kyori:adventure-text-serializer-ansi:4.21.0') {
        exclude(module: 'adventure-bom')
        exclude(module: 'adventure-api')
        exclude(module: 'annotations')
    }
    api('net.kyori:ansi:1.1.1') {
        exclude(module: 'annotations')
    }

    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.testcontainers:junit-jupiter:1.21.1'
    testImplementation 'org.mockito:mockito-core:5.18.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.18.0'
    testImplementation 'org.awaitility:awaitility:4.3.0'

    testImplementation 'com.zaxxer:HikariCP:6.3.0'
    testImplementation 'redis.clients:jedis:5.2.0'
    testImplementation 'io.nats:jnats:2.21.1'
    testImplementation 'com.rabbitmq:amqp-client:5.25.0'
    testImplementation 'org.postgresql:postgresql:42.7.6'
    testImplementation 'com.h2database:h2:2.1.214'
    testImplementation 'org.xerial:sqlite-jdbc:3.49.1.0'
    testImplementation 'com.mysql:mysql-connector-j:9.3.0'
    testImplementation 'org.mariadb.jdbc:mariadb-java-client:3.5.2'
    testImplementation 'org.mongodb:mongodb-driver-legacy:5.5.0'
    testImplementation 'me.lucko.configurate:configurate-toml:3.7'
    testImplementation 'org.spongepowered:configurate-yaml:3.7.3'
    testImplementation 'org.spongepowered:configurate-hocon:3.7.3'
    testImplementation 'org.yaml:snakeyaml:1.22'
    testImplementation 'net.luckperms:rest-api-java-client:0.1'
}

shadowJar {
    archiveFileName = 'luckperms-standalone.jarinjar'

    dependencies {
        exclude(dependency('org.slf4j:slf4j-api:.*'))
        exclude(dependency('org.apache.logging.log4j:log4j-api:.*'))
        exclude(dependency('org.apache.logging.log4j:log4j-core:.*'))
        exclude(dependency('org.apache.logging.log4j:log4j-slf4j-impl:.*'))
        exclude(dependency('net.minecrell:terminalconsoleappender:.*'))
        exclude(dependency('org.jline:jline-terminal-jansi:.*'))
    }

    relocate 'net.kyori.event', 'me.lucko.luckperms.lib.eventbus'
    relocate 'com.github.benmanes.caffeine', 'me.lucko.luckperms.lib.caffeine'
    relocate 'okio', 'me.lucko.luckperms.lib.okio'
    relocate 'okhttp3', 'me.lucko.luckperms.lib.okhttp3'
    relocate 'net.bytebuddy', 'me.lucko.luckperms.lib.bytebuddy'
    relocate 'me.lucko.commodore', 'me.lucko.luckperms.lib.commodore'
    relocate 'org.mariadb.jdbc', 'me.lucko.luckperms.lib.mariadb'
    relocate 'com.mysql', 'me.lucko.luckperms.lib.mysql'
    relocate 'org.postgresql', 'me.lucko.luckperms.lib.postgresql'
    relocate 'com.zaxxer.hikari', 'me.lucko.luckperms.lib.hikari'
    relocate 'com.mongodb', 'me.lucko.luckperms.lib.mongodb'
    relocate 'org.bson', 'me.lucko.luckperms.lib.bson'
    relocate 'redis.clients.jedis', 'me.lucko.luckperms.lib.jedis'
    relocate 'io.nats.client', 'me.lucko.luckperms.lib.nats'
    relocate 'com.rabbitmq', 'me.lucko.luckperms.lib.rabbitmq'
    relocate 'org.apache.commons.pool2', 'me.lucko.luckperms.lib.commonspool2'
    relocate 'ninja.leaping.configurate', 'me.lucko.luckperms.lib.configurate'
    relocate 'org.yaml.snakeyaml', 'me.lucko.luckperms.lib.yaml'
}

artifacts {
    archives shadowJar
}
