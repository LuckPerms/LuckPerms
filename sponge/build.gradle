plugins {
    alias(libs.plugins.shadow)
}

sourceCompatibility = 17
targetCompatibility = 21

repositories {
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
}

dependencies {
    implementation project(':common')
    implementation project(':sponge:sponge-service')
    implementation project(':sponge:sponge-service-proxy')
    compileOnly project(':common:loader-utils')

    compileOnly('org.spongepowered:spongeapi:12.0.0') {
        exclude(module: 'configurate-core')
        exclude(module: 'configurate-hocon')
        exclude(module: 'configurate-gson')
        exclude(module: 'configurate-yaml')
    }
    compileOnly 'com.google.guava:guava:33.3.1-jre'
}

processResources {
    filesMatching('META-INF/sponge_plugins.json') {
        expand 'pluginVersion': project.ext.fullVersion
    }
}

shadowJar {
    archiveFileName = 'luckperms-sponge.jarinjar'

    dependencies {
        include(dependency('me.lucko.luckperms:.*'))
    }

    //relocate 'net.kyori.adventure', 'me.lucko.luckperms.lib.adventure'
    relocate 'net.kyori.event', 'me.lucko.luckperms.lib.eventbus'
    relocate 'com.github.benmanes.caffeine', 'me.lucko.luckperms.lib.caffeine'
    relocate 'okio', 'me.lucko.luckperms.lib.okio'
    relocate 'okhttp3', 'me.lucko.luckperms.lib.okhttp3'
    relocate 'net.bytebuddy', 'me.lucko.luckperms.lib.bytebuddy'
    relocate 'me.lucko.commodore', 'me.lucko.luckperms.lib.commodore'
    relocate 'org.mariadb.jdbc', 'me.lucko.luckperms.lib.mariadb'
    relocate 'com.mysql', 'me.lucko.luckperms.lib.mysql'
    relocate 'org.postgresql', 'me.lucko.luckperms.lib.postgresql'
    relocate 'com.zaxxer.hikari', 'me.lucko.luckperms.lib.hikari'
    relocate 'com.mongodb', 'me.lucko.luckperms.lib.mongodb'
    relocate 'org.bson', 'me.lucko.luckperms.lib.bson'
    relocate 'redis.clients.jedis', 'me.lucko.luckperms.lib.jedis'
    relocate 'io.nats.client', 'me.lucko.luckperms.lib.nats'
    relocate 'com.rabbitmq', 'me.lucko.luckperms.lib.rabbitmq'
    relocate 'org.apache.commons.pool2', 'me.lucko.luckperms.lib.commonspool2'
    relocate 'ninja.leaping.configurate', 'me.lucko.luckperms.lib.configurate'
}

artifacts {
    archives shadowJar
}
